
plugins {
    id 'com.android.application'
    alias libs.plugins.kotlin.android
}        

android {
    if (System.getenv('SIGNING_KEY_ALIAS') != null) {
        signingConfigs {
            twinmeRelease {
                keyAlias System.getenv('SIGNING_KEY_ALIAS')
                keyPassword System.getenv('SIGNING_KEY_KEY_PASSWORD')
                storeFile file(System.getenv('SIGNING_KEY_STORE_FILE'))
                storePassword System.getenv('SIGNING_KEY_STORE_PASSWORD')
            }
        }
    }
    compileSdkVersion libs.versions.compile.sdk.version.get()
    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        applicationId "org.twinlife.device.android.twinme"
        minSdkVersion libs.versions.min.sdk.version.get() as int
        targetSdkVersion libs.versions.target.sdk.version.get() as int
        versionCode 499
        versionName "31.5"
        manifestPlaceholders.domainName = "twin.me"

        resourceConfigurations += ["en", "ar", "da", "de", "es", "fa", "fi", "fr", "hi", "in", "it", "iw", "ja", "ko", "nb", "nl", "pl", "pt", "ru", "sv", "tr", "uk", "vi", "zh-rCN", "zh-rTW"]
    }
    namespace 'org.twinlife.device.android.twinme'
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        // Production settings and build parameters.
        // The APK/AAB must be produced with this build type.
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix ""

            buildConfigField "String", 'APPLICATION_ID_SUFFIX', '""'
            buildConfigField "String", 'ACCOUNT_ACTIVITY', '"org.twinlife.twinme.ui.AccountActivity"'
            buildConfigField "String", "CHECK_VERSION_URL", "\"https://twin.me/download/twinme.json\""
            buildConfigField "String", "FILE_PROVIDER", '"org.twinlife.device.android.twinme.fileprovider"'
            buildConfigField "String", "FLAVOR", '""'
            buildConfigField "boolean", "DISPLAY_PREMIUM_FEATURE", "true"
            buildConfigField "String", "INVITATION_PARAM_ID", '"twincodeId"'

            manifestPlaceholders.domainName = "twin.me"
            manifestPlaceholders.twinmePackage = "org.twinlife.device.android.twinme"

            if (System.getenv('SIGNING_KEY_ALIAS') != null) {
                signingConfig signingConfigs.twinmeRelease
            }
        }

        // Production settings, dev build parameters.
        // Use this build type to test the app on the production environment when making a release.
        staging {
            initWith buildTypes.release
            shrinkResources false
            minifyEnabled false
            debuggable true

            signingConfig signingConfigs.debug

            applicationIdSuffix ".staging"
            buildConfigField "String", 'APPLICATION_ID_SUFFIX', '".staging"'
        }

        // Dev settings and build parameters.
        // Use to run the app during the development cycle.
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "String", 'APPLICATION_ID_SUFFIX', '".debug"'

            buildConfigField "String", 'ACCOUNT_ACTIVITY', '"org.twinlife.twinme.ui.DebugAccountActivity"'
            buildConfigField "String", "CHECK_VERSION_URL", "\"https://invite.mytwinlife.net/download/twinme.json\""
            buildConfigField "boolean", "DISPLAY_PREMIUM_FEATURE", "true"
            buildConfigField "String", "FILE_PROVIDER", '"org.twinlife.device.android.twinme.fileprovider"'
            buildConfigField "String", "FLAVOR", '"Development"'
            buildConfigField "String", "INVITATION_PARAM_ID", '"twincodeId"'

            manifestPlaceholders.domainName = "mytwinlife.net"
            manifestPlaceholders.twinmePackage = "org.twinlife.device.android.twinme.debug"
        }
    }

    flavorDimensions = ["product", "tier"]

    productFlavors {
        // Needed to setup twinlife & twinme product dimension
        skred {
            dimension "product"
        }
        twinme {
            dimension "product"
        }

        //Skred and Twinme
        free {
            dimension "tier"
        }

        //Twinme+
        '+' {
            dimension "tier"
        }
    }

    // Only define necessary variants
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("skred")) {
            if(names.contains("+") || variant.buildType.name == 'debug' ) {
                // skred doesn't have a "plus" variant, nor a debug buildType
                setIgnore(true)
            }
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }
}
dependencies {

    implementation project(':twinlife-framework')
    implementation project(':webrtc-framework')
    implementation project(':twinme-framework')
    implementation project(':twinme-android-common')
    implementation project(':zxing')
    implementation project(':image-cropper')

    implementation libs.androidx.appcompat
    implementation libs.androidx.recyclerview
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.media
    implementation libs.androidx.core.splashscreen
    implementation libs.androidx.browser
    implementation libs.lottie
    implementation libs.glide
    implementation libs.glide.recyclerview

    implementation libs.installreferrer

    coreLibraryDesugaring libs.desugar.jdk.libs

    // Support for Chooser Target (Replaced by direct share targets in Android 10)
    runtimeOnly libs.androidx.sharetarget

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation libs.leakcanary.android
}
